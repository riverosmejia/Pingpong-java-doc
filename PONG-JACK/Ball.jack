class Ball {
    field int x;
    field int y;
    field int dx;
    field int dy;
    field int d;    // diámetro
    field int speed;

    constructor Ball new(int xPos, int yPos, int dirX, int dirY) {
        let x = xPos;
        let y = yPos;
        let dx = dirX;
        let dy = dirY;
        let d = 8;
        let speed = 2;
        return this;
    }

    method void draw() {
        do Screen.setColor(true);
        do Screen.drawCircle(x, y, d / 2);
        return;
    }

    method void erase() {
        do Screen.setColor(false);
        do Screen.drawCircle(x, y, d / 2);
        return;
    }

    method boolean overlapsWith(Player p) {
        var int px, py, pw, ph;
        var int bx1, by1, bx2, by2;

        let px = p.getX();
        let py = p.getY();
        let pw = p.getWidth();
        let ph = p.getHeight();

        let bx1 = x;
        let by1 = y;
        let bx2 = x + d;
        let by2 = y + d;

        if ((bx2 < px) | (bx1 > (px + pw)) | (by2 < py) | (by1 > (py + ph))) {
            return false;
        }

        return true;
    }

    // Actualiza la posición y rebotes contra top/bottom
    method void update() {
        var int newX, newY;
        
        do erase();
        
        // Calcular nueva posición
        let newX = x + (dx * speed);
        let newY = y + (dy * speed);

        // Aplicar límites en X (con radio de la pelota)
        if (newX < (d / 2)) { 
            let newX = d / 2;
        }
        if (newX > (511 - (d / 2))) { 
            let newX = 511 - (d / 2);
        }

        // Rebotes verticales (con radio de la pelota)
        if (newY < (d / 2)) { 
            let newY = d / 2;
            let dy = -dy;
        }
        if (newY > (255 - (d / 2))) { 
            let newY = 255 - (d / 2);
            let dy = -dy;
        }

        // Actualizar posición
        let x = newX;
        let y = newY;

        do draw();
        return;
    }

    method void invertX() {
        let dx = -dx;
        return;
    }

    method void invertY() {
        let dy = -dy;
        return;
    }

    method void moveTo(int newX, int newY) {
        let x = newX;
        let y = newY;
        return;
    }

    method void reset(int cx, int cy, int dirX, int dirY) {
        do erase();  // Borrar la pelota en su posición actual
        let x = cx;
        let y = cy;
        let dx = dirX;
        let dy = dirY;
        do draw();   // Dibujar en la nueva posición
        return;
    }

    // getters
    method int getX() { return x; }
    method int getY() { return y; }
    method int getDiameter() { return d; }
}
